# Licensed under the MIT License
# https://github.com/craigahobbs/ollama-chat/blob/main/LICENSE

include <args.mds>
include <forms.mds>

include 'ollamaChatConversation.bare'
include 'ollamaChatModels.bare'
include 'ollamaChatTemplate.bare'


# The Ollama Chat application main entry point
async function ollamaChatMain():
    args = argsParse(ollamaChatArguments)

    # Start a conversation?
    conversationMessage = stringTrim(objectGet(args, 'message', ''))
    if conversationMessage:
        ollamaChatStartConversation(conversationMessage)
        return
    endif

    # Start a template?
    templateName = stringTrim(objectGet(args, 'template', ''))
    if templateName:
        ollamaChatStartTemplate(args, templateName)
        return
    endif

    # Render the page
    view = objectGet(args, 'view')
    if view == 'chat':
        ollamaChatConversationPage(args)
    elif view == 'download':
        ollamaChatModelsDownloadPage(args)
    elif view == 'models':
        ollamaChatModelsPage(args)
    elif view == 'template':
        ollamaChatTemplatePage(args)
    elif view == 'variables':
        ollamaChatTemplateVariablesPage(args)
    else:
        ollamaChatIndexPage(args)
    endif
endfunction


# The Ollama Chat application URL arguments
ollamaChatArguments = argsValidate(arrayNew( \
    objectNew('name', 'action', 'explicit', true), \
    objectNew('name', 'actionID', 'explicit', true), \
    objectNew('name', 'filterCompatible', 'type', 'bool', 'default', true), \
    objectNew('name', 'filterMonths', 'type', 'int', 'default', 3), \
    objectNew('name', 'id'), \
    objectNew('name', 'message', 'explicit', true), \
    objectNew('name', 'multiline', 'type', 'bool'), \
    objectNew('name', 'sort', 'type', 'string', 'default', 'Modified'), \
    objectNew('name', 'template', 'explicit', true), \
    objectNew('name', 'templateVars', 'explicit', true), \
    objectNew('name', 'text', 'type', 'bool'), \
    objectNew('name', 'view') \
))


# The application title
ollamaChatTitle = 'Ollama Chat'


# The Ollama Chat error page
function ollamaChatErrorPage(message):
    documentSetTitle(ollamaChatTitle)
    markdownPrint( \
        argsLink(ollamaChatArguments, 'Back', null, true), \
        '', \
        '# ' + markdownEscape(ollamaChatTitle), \
        '', \
        '**ERROR:** ' + markdownEscape(message) \
    )
endfunction


# The Ollama Chat start conversation command
async function ollamaChatStartConversation(message):
    # Start the conversation
    startArgs = objectNew('user', message)
    startResponse = systemFetch(objectNew('url', 'startConversation', 'body', jsonStringify(startArgs)))
    startResponse = if(startResponse != null, jsonParse(startResponse))
    conversationID = if(startResponse != null, objectGet(startResponse, 'id'))

    # Navigate to the started conversation
    windowSetLocation(argsURL(ollamaChatArguments, objectNew('view', 'chat', 'id', conversationID)))
endfunction


# The Ollama Chat start template command
async function ollamaChatStartTemplate(args, templateName):
    # Find the template ID
    templateID = null
    conversationsResponse = systemFetch('getConversations')
    conversationsResponse = if(conversationsResponse != null, jsonParse(conversationsResponse))
    if conversationsResponse != null:
        for template in objectGet(conversationsResponse, 'templates'):
            if objectGet(template, 'name') == templateName:
                templateID = objectGet(template, 'id')
                break
            endif
        endfor
    endif

    # Unknown template name?
    if templateID == null:
        ollamaChatErrorPage('Unknown template name: ' + templateName)
        return
    endif

    # Parse the JSON variables
    templateVars = jsonParse(objectGet(args, 'templateVars', '{}'))
    if templateVars == null:
        ollamaChatErrorPage('Invalid template variables: ' + objectGet(args, 'templateVars'))
        return
    endif

    # Start the template
    startArgs = objectNew('id', templateID, 'variables', templateVars)
    startResponse = systemFetch(objectNew('url', 'startTemplate', 'body', jsonStringify(startArgs)))
    startResponse = if(startResponse != null, jsonParse(startResponse))
    conversationID = if(startResponse != null, objectGet(startResponse, 'id'))
    if conversationID == null:
        ollamaChatErrorPage('Missing one or more template variables')
        return
    endif

    # Navigate to the started conversation
    windowSetLocation(argsURL(ollamaChatArguments, objectNew('view', 'chat', 'id', conversationID)))
endfunction


# The Ollama Chat index page
async function ollamaChatIndexPage(args):
    action = objectGet(args, 'action')
    actionID = objectGet(args, 'actionID')

    # Get the conversations
    conversationsResponse = systemFetch('getConversations')
    conversationsResponse = if(conversationsResponse != null, jsonParse(conversationsResponse))
    if conversationsResponse == null:
        ollamaChatErrorPage('Failed to get conversations')
        return
    endif

    # Render the title
    documentSetTitle(ollamaChatTitle)
    markdownPrint('# ' + markdownEscape(ollamaChatTitle))

    # Render the menu
    spacer = stringFromCharCode(160, 160)
    indent = spacer + spacer
    markdownPrint( \
        '**Model:** ' + objectGet(conversationsResponse, 'model') + spacer + \
            argsLink(ollamaChatArguments, 'Select', objectNew('view', 'models')), \
        '', \
        argsLink(ollamaChatArguments, 'Start Conversation', objectNew('view', 'chat')) \
    )
    elementModelRender(objectNew( \
        'html', 'p', \
        'elem',  formsLinkButtonElements('Add Template', ollamaChatIndexOnTemplate) \
    ))

    # Render the conversations
    conversations = objectGet(conversationsResponse, 'conversations')
    markdownPrint('', '## Conversations')
    if !conversations:
        markdownPrint('', indent + '*No conversations*')
    else:
        conversationTableRows = arrayNew()
        conversationTable = arrayNew( \
            objectNew('html', 'table', 'elem', arrayNew( \
                objectNew('html', 'tbody', 'elem', conversationTableRows) \
            )) \
        )
        for conversation in conversations:
            id = objectGet(conversation, 'id')
            title = objectGet(conversation, 'title')
            model = objectGet(conversation, 'model')
            selected = action == 'conversation' && actionID == id
            conversationURL = argsURL(ollamaChatArguments, objectNew('view', 'chat', 'id', id))
            selectText = if(selected, 'Cancel', 'Select')
            selectURL = argsURL(ollamaChatArguments, objectNew('action', if(!selected, 'conversation'), 'actionID', if(!selected, id)))
            arrayPush(conversationTableRows, objectNew('html', 'tr', 'elem', arrayNew( \
                objectNew('html', 'td', 'elem', formsLinkElements(title, conversationURL)), \
                objectNew('html', 'td', 'elem', objectNew('text', model)), \
                objectNew('html', 'td', 'elem', arrayNew( \
                    formsLinkElements(selectText, selectURL), \
                    if(selected, arrayNew( \
                        objectNew('text', spacer + '|' + spacer), \
                        formsLinkButtonElements('Up', systemPartial(ollamaChatIndexOnConversationAction, args, id, 'up')), \
                        objectNew('text', spacer), \
                        formsLinkButtonElements('Down', systemPartial(ollamaChatIndexOnConversationAction, args, id, 'down')), \
                        objectNew('text', spacer), \
                        formsLinkButtonElements('Delete', systemPartial(ollamaChatIndexOnConversationAction, args, id, 'delete')) \
                    )) \
                )) \
            )))
        endfor
        elementModelRender(conversationTable)
    endif

    # Render the conversation templates
    templates = objectGet(conversationsResponse, 'templates')
    markdownPrint('', '## Templates')
    if !templates:
        markdownPrint('', indent + '*No templates*')
    else:
        templateTableRows = arrayNew()
        templateTable = arrayNew( \
            objectNew('html', 'table', 'elem', arrayNew( \
                objectNew('html', 'tbody', 'elem', templateTableRows) \
            )) \
        )
        for template in templates:
            templateID = objectGet(template, 'id')
            templateTitle = objectGet(template, 'title')
            templateStartFn = systemPartial(ollamaChatIndexOnTemplateSelect, templateID)
            selected = action == 'template' && actionID == templateID
            selectText = if(selected, 'Cancel', 'Select')
            selectURL = argsURL(ollamaChatArguments, objectNew('action', if(!selected, 'template'), 'actionID', if(!selected, templateID)))
            arrayPush(templateTableRows, objectNew('html', 'tr', 'elem', arrayNew( \
                objectNew('html', 'td', 'elem', formsLinkButtonElements(templateTitle, templateStartFn)), \
                objectNew('html', 'td', 'elem', arrayNew( \
                    formsLinkElements(selectText, selectURL), \
                    if(selected, arrayNew( \
                        objectNew('text', spacer + '|' + spacer), \
                        formsLinkButtonElements('Up', systemPartial(ollamaChatIndexOnTemplateAction, args, templateID, 'up')), \
                        objectNew('text', spacer), \
                        formsLinkButtonElements('Down', systemPartial(ollamaChatIndexOnTemplateAction, args, templateID, 'down')), \
                        objectNew('text', spacer), \
                        formsLinkElements('Edit', argsURL(ollamaChatArguments, objectNew('view', 'template', 'id', templateID))), \
                        objectNew('text', spacer), \
                        formsLinkButtonElements('Delete', systemPartial(ollamaChatIndexOnTemplateAction, args, templateID, 'delete')) \
                    )) \
                )) \
            )))
        endfor
        elementModelRender(templateTable)
    endif
endfunction


# Add template on-click event handler
async function ollamaChatIndexOnTemplate():
    # Create the new template
    templateRequest = objectNew( \
        'title', 'New template', \
        'prompts', arrayNew('New prompt') \
    )
    templateResponse = systemFetch(objectNew('url', 'createTemplate', 'body', jsonStringify(templateRequest)))
    templateResponse = if(templateResponse != null, jsonParse(templateResponse))
    templateID = if(templateResponse != null, objectGet(templateResponse, 'id'))

    # Edit the new template
    if templateID != null:
        windowSetLocation(argsURL(ollamaChatArguments, objectNew('id', templateID, 'view', 'template')))
    endif
endfunction


# Conversation action on-click event handler
async function ollamaChatIndexOnConversationAction(args, id, action):
    if action == 'delete':
        systemFetch(objectNew('url', 'deleteConversation', 'body', jsonStringify(objectNew('id', id))))
        windowSetLocation(argsURL(ollamaChatArguments))
    else:
        systemFetch(objectNew('url', 'moveConversation', 'body', jsonStringify(objectNew('id', id, 'down', action == 'down'))))
        ollamaChatIndexPage(args)
    endif
endfunction


# Template action on-click event handler
async function ollamaChatIndexOnTemplateAction(args, id, action):
    if action == 'delete':
        systemFetch(objectNew('url', 'deleteTemplate', 'body', jsonStringify(objectNew('id', id))))
        windowSetLocation(argsURL(ollamaChatArguments))
    else:
        systemFetch(objectNew('url', 'moveTemplate', 'body', jsonStringify(objectNew('id', id, 'down', action == 'down'))))
        ollamaChatIndexPage(args)
    endif
endfunction


# Select template on-click event handler
async function ollamaChatIndexOnTemplateSelect(templateID):
    # Get the template
    templateResponse = systemFetch('getTemplate?id=' + templateID)
    templateResponse = if(templateResponse != null, jsonParse(templateResponse))
    if templateResponse == null:
        return
    endif

    # Variables?
    if objectGet(templateResponse, 'variables'):
        windowSetLocation(argsURL(ollamaChatArguments, objectNew('view', 'variables', 'id', templateID)))
        return
    endif

    # Start the conversation
    startRequest = objectNew('id', templateID)
    startResponse = systemFetch(objectNew('url', 'startTemplate', 'body', jsonStringify(startRequest)))
    startResponse = if(startResponse != null, jsonParse(startResponse))
    if startResponse == null:
        return
    endif
    id = objectGet(startResponse, 'id')

    # Navigate to the conversation page
    windowSetLocation(argsURL(ollamaChatArguments, objectNew('view', 'chat', 'id', id)))
endfunction
